  ' Ambil angka dari teks "CPO process : 773 MT"
    Function ExtractCPOVal(ByVal cellVal As String) As Double
        Dim parts() As String, i As Long
        ExtractCPOVal = 0
        cellVal = Trim(cellVal)
        If Len(cellVal) = 0 Then Exit Function
        parts = Split(cellVal, " ")
        For i = LBound(parts) To UBound(parts)
            If IsNumeric(parts(i)) Then
                ExtractCPOVal = CDbl(parts(i))
                Exit Function
            End If
        Next i
    End Function
        
    
' ========================
' Generate Daily Storage CPO & CPKO
' ========================
Sub GenerateDailyStorageCPOCPKO(wsDest As Worksheet, ByRef currentRow As Long, targetDate As Date)
    Dim cpoFilePath As String, cpkoFilePath As String
    Dim sheetCodeCPO As String, sheetCodeCPKO As String
    Dim monthName As String, folderCPKO As String, folderCPO As String
    Dim qtyCons As Double, qtyBulk As Double, qtyHigh As Double
    Dim cpoTotal As Double, cpkoTotal As Double
    
    
    ' --- format kode sheet di file sumber ---
    sheetCodeCPO = Format(targetDate, "ddmm")   ' contoh: "2209"
    sheetCodeCPKO = Format(targetDate, "dd")    ' contoh: "22"
    
    ' --- Buat nama bulan Indonesia ---
    monthName = GetIndonesianMonth(Month(targetDate), Year(targetDate))
    
    ' --- File CPO (Palmitic) ---
    folderCPO = "Z:\Document Share (Marunda All)\Quality Management\1. Daily Storage\DAILY STORAGE 2025\Daily Storage 2025 (Palmitic)\"
    cpoFilePath = folderCPO & "8. DAILY STORAGE " & monthName & " (Palmitic Base).xlsx"
    If Dir(cpoFilePath) = "" Then
        cpoFilePath = CariFileDailyStorage(folderCPO, monthName, "PALMITIC")
    End If
    
    ' --- File CPKO (Non Palmitic) ---
    folderCPKO = "Z:\Document Share (Marunda All)\Quality Management\1. Daily Storage\DAILY STORAGE 2025\"
    cpkoFilePath = folderCPKO & "07. Daily Storage " & monthName & " (Non Palmitic Base).xlsx"
    If Dir(cpkoFilePath) = "" Then
        cpkoFilePath = CariFileDailyStorage(folderCPKO, monthName, "NON PALMITIC")
    End If
    
    ' --- Hitung CPO ---
    Call GetCPOCPKOValue(cpoFilePath, sheetCodeCPO, "CPO", cpoTotal, qtyCons, qtyBulk, qtyHigh)
    
    ' --- Hitung CPKO ---
    Call GetCPOCPKOValue(cpkoFilePath, sheetCodeCPKO, "CPKO", cpkoTotal, 0, 0, 0)
    
    ' --- Output ke sheet target ---
    
    wsDest.Cells(currentRow, 1).Value = "CPO Stock"
    currentRow = currentRow + 1
    wsDest.Cells(currentRow, 1).Value = "- Consumer : " & Format(qtyCons / 1000, "#,##0") & " MT"
    currentRow = currentRow + 1
    wsDest.Cells(currentRow, 1).Value = "- Bulk     : " & Format(qtyBulk / 1000, "#,##0") & " MT"
    currentRow = currentRow + 1
    wsDest.Cells(currentRow, 1).Value = "- High FFA : " & Format(qtyHigh / 1000, "#,##0") & " MT"
    currentRow = currentRow + 2
    
    wsDest.Cells(currentRow, 1).Value = "CPKO Stock : " & Format(cpkoTotal / 1000, "#,##0") & " MT"
    currentRow = currentRow + 2
End Sub

   
   
Private Function CariFileDailyStorage(folderPath As String, bulanTahun As String, keyword As String) As String
    Dim f As String, targetNama As String
    targetNama = UCase("Daily Storage " & bulanTahun)
    
    f = Dir(folderPath & "*.xlsx")
    Do While f <> ""
        If InStr(1, UCase(f), targetNama) > 0 _
           And InStr(1, UCase(f), UCase(keyword)) > 0 Then
           CariFileDailyStorage = folderPath & f
           Exit Function
        End If
        f = Dir
    Loop
    CariFileDailyStorage = ""
End Function

Private Function GetIndonesianMonth(ByVal bulan As Integer, ByVal tahun As Integer) As String
    Dim namaBulan As Variant
    namaBulan = Array("", "Januari", "Februari", "Maret", "April", "Mei", "Juni", _
                          "Juli", "Agustus", "September", "Oktober", "November", "Desember")
    GetIndonesianMonth = namaBulan(bulan) & " " & tahun
End Function
   
' ========================
' Ambil nilai CPO / CPKO
' ========================
Sub GetCPOCPKOValue(filePath As String, sheetCode As String, labelName As String, _
                    ByRef totalStock As Double, _
                    ByRef qtyConsumer As Double, ByRef qtyBulk As Double, _
                    ByRef qtyHighFFA As Double)

    Dim wb As Workbook, ws As Worksheet
    Dim colQty As String, colFFA As String
    Dim rowStart As Long, rowEnd As Long, i As Long
    Dim q As Variant, ffa As Variant
    
    qtyConsumer = 0: qtyBulk = 0: qtyHighFFA = 0
    totalStock = 0
    
    If Dir(filePath) = "" Then Exit Sub
    
    Set wb = Workbooks.Open(filePath, readOnly:=True)
    On Error Resume Next
    Set ws = wb.Sheets(sheetCode)
    On Error GoTo 0
    
    If ws Is Nothing Then
        wb.Close False
        Exit Sub
    End If
    
    If labelName = "CPO" Then
        colQty = "G": colFFA = "I": rowStart = 8: rowEnd = 23
    ElseIf labelName = "CPKO" Then
        colQty = "F": colFFA = "H": rowStart = 8: rowEnd = 17
    End If
    
    For i = rowStart To rowEnd
        q = ws.Range(colQty & i).Value
        ffa = ws.Range(colFFA & i).Value
        
If IsNumeric(q) And q > 0 Then
    If labelName = "CPO" Then
        If Trim(ws.Range(colFFA & i).Value & "") = "" Then
            ' Kalau kosong ? masuk kategori FFA tidak ada
            qtyNoFFA = qtyNoFFA + q
        ElseIf IsNumeric(ffa) Then
            If ffa < 3.5 Then
                qtyConsumer = qtyConsumer + q
            ElseIf ffa >= 3.5 And ffa <= 5 Then
                qtyBulk = qtyBulk + q
            ElseIf ffa > 5 Then
                qtyHighFFA = qtyHighFFA + q
            End If
        End If
    ElseIf labelName = "CPKO" Then
        totalStock = totalStock + q  'langsung akumulasi semua
    End If
End If
    Next i
    
    If labelName = "CPO" Then
        totalStock = qtyConsumer + qtyBulk + qtyHighFFA
    End If
    
    wb.Close False
End Sub
