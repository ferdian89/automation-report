Sub GetO136FromEmail_Ferry_ToTodaySheet()
    Dim olApp As Object, olNs As Object, olFolder As Object
    Dim olMail As Object, olAtmt As Object
    Dim xlApp As Object, xlWb As Object, xlWs As Object, sh As Object
    Dim wsDest As Worksheet
    Dim tempFile As String
    Dim foundValue As Variant
    Dim todaySheetName As String
    Dim lastRow As Long

    ' Parameter pencarian
    Dim targetSender As String
    Dim targetDate As Date
    Dim targetAttachment As String

    targetSender = "Ferry Hermansyah Lubis"
    targetDate = DateSerial(2025, 7, 24)
    targetAttachment = "02.Daily Report SCD  Hydro Plant (HO) Juli 2025.xlsx"

    ' Nama sheet berdasarkan tanggal hari ini (tanpa bulan/tahun)
    todaySheetName = CStr(Day(Date))

    ' Buat atau ambil sheet tujuan
    On Error Resume Next
    Set wsDest = ThisWorkbook.Sheets(todaySheetName)
    If wsDest Is Nothing Then
        Set wsDest = ThisWorkbook.Sheets.Add
        wsDest.Name = todaySheetName
    End If
    On Error GoTo 0

    ' Siapkan Outlook
    Set olApp = GetObject(, "Outlook.Application")
    Set olNs = olApp.GetNamespace("MAPI")
    Set olFolder = olNs.GetDefaultFolder(6) ' Inbox

    For Each olMail In olFolder.Items
        If TypeName(olMail) = "MailItem" Then
            If Int(olMail.ReceivedTime) = targetDate Then
                If olMail.SenderName = targetSender Then
                    If olMail.Attachments.Count > 0 Then
                        For Each olAtmt In olMail.Attachments
                            If olAtmt.fileName = targetAttachment Then

                                ' Simpan attachment sementara
                                tempFile = Environ("TEMP") & "\" & olAtmt.fileName
                                On Error Resume Next
                                Kill tempFile
                                On Error GoTo 0
                                olAtmt.SaveAsFile tempFile

                                ' Buka file Excel, baca SCD!O136
                                Set xlApp = CreateObject("Excel.Application")
                                xlApp.Visible = False
                                Set xlWb = xlApp.Workbooks.Open(tempFile, ReadOnly:=True)

                                Set xlWs = Nothing
                                For Each sh In xlWb.Sheets
                                    If Trim(sh.Name) = "SCD" Then
                                        Set xlWs = sh
                                        Exit For
                                    End If
                                Next

                                If Not xlWs Is Nothing Then
                                    foundValue = xlWs.Range("O136").Value
                                Else
                                    foundValue = "Sheet 'SCD' not found"
                                End If

                                xlWb.Close False
                                xlApp.Quit
                                Set xlApp = Nothing

                                ' Tulis ke sheet tujuan
                                lastRow = wsDest.Cells(wsDest.Rows.Count, 1).End(xlUp).Row + 1
                                wsDest.Cells(lastRow, 1).Value = olAtmt.fileName
                                If IsNumeric(foundValue) Then
                                    wsDest.Cells(lastRow, 2).Value = foundValue
                                Else
                                    wsDest.Cells(lastRow, 2).Value = "Bukan angka atau tidak ditemukan"
                                End If

                                MsgBox "Selesai. Nilai O136: " & foundValue, vbInformation
                                Exit Sub
                            End If
                        Next
                    End If
                End If
            End If
        End If
    Next

    MsgBox "Email dengan kriteria tidak ditemukan.", vbExclamation

End Sub

