Sub GenerateMarsho()
    Dim filePath As String
    Dim targetDate As Date, targetDay As Integer
    Dim wsTarget As Worksheet
    Dim startRow As Long
    Dim wb As Workbook
    Dim ws As Worksheet

    ' Tentukan tanggal proses (kemarin)
    targetDate = Date - 1
    targetDay = Day(targetDate)
    
    ' Ambil file dari email hari ini
    filePath = GetAttachment_BIO(targetDate)
    If filePath = "" Then
        MsgBox "File BIO tidak ditemukan dari email hari ini.", vbExclamation
        Exit Sub
    End If
    
    ' Pastikan sheet output ada
    On Error Resume Next
    Set wsTarget = ThisWorkbook.Sheets(Format(targetDate, "dd"))
    If wsTarget Is Nothing Then
        Set wsTarget = ThisWorkbook.Sheets.Add(After:=ThisWorkbook.Sheets(ThisWorkbook.Sheets.Count))
        wsTarget.Name = Format(targetDate, "dd")
    End If
    On Error GoTo 0
    
    ' Bersihkan area lama
    wsTarget.Cells.Clear
    
    ' Baris mulai output
    startRow = 1
    
    ' --- Buka workbook sumber ---
    Set wb = Workbooks.Open(filePath)
    On Error Resume Next
    Set ws = wb.Sheets("Marunda-Marsho")
    If ws Is Nothing Then
        MsgBox "Sheet 'Marunda-Marsho' tidak ditemukan di file sumber.", vbExclamation
        wb.Close False
        Exit Sub
    End If
    On Error GoTo 0
    
    ' --- Jalankan GenerateMarshoPlantReport dengan worksheet ---
    GenerateMarshoPlantReport ws, wsTarget, targetDate, startRow
    
    ' Tutup workbook sumber
    wb.Close False

    MsgBox "? Laporan aktual planning untuk tanggal " & Format(targetDate, "dd MMM yyyy") & " berhasil dibuat!", vbInformation
End Sub


Sub GenerateMarshoPlantReport(ws As Worksheet, wsTarget As Worksheet, targetDate As Date, ByRef row As Long)
    Dim col As Long, lastCol As Long
    Dim lastRow As Long
    Dim found As Boolean
    Dim targetText As String
    Dim dataCol As Long
    Dim planValue As Variant, actualValue As Variant
    Dim outputText As String
    
    ' --- ubah tanggal ke format "d - mmm" ---
    targetText = Format(targetDate, "d - mmm")
    
    ' --- cari kolom tanggal di row 5 ---
    lastCol = ws.Cells(5, ws.Columns.Count).End(xlToLeft).Column
    found = False
    For col = 1 To lastCol
        If ws.Cells(5, col).Text = targetText Then
            dataCol = col
            found = True
            Exit For
        End If
    Next col
    
    If Not found Then
        wsTarget.Cells(row, 1).Value = "Marsho Plant - Tanggal tidak ditemukan"
        row = row + 2
        Exit Sub
    End If
    
' --- ambil angka terakhir di bawah Plan (row 6) ---
lastRow = ws.Cells(ws.Rows.Count, dataCol).End(xlUp).row
planValue = CLng(ws.Cells(lastRow, dataCol).Value)  ' ubah ke bulat

' --- ambil angka terakhir di bawah Actual (row 7) ---
lastRow = ws.Cells(ws.Rows.Count, dataCol + 1).End(xlUp).row
actualValue = CLng(ws.Cells(lastRow, dataCol + 1).Value)  ' ubah ke bulat

' --- buat teks output dan tulis di wsTarget ---
wsTarget.Cells(row, 1).Value = "Marsho Plant"
wsTarget.Cells(row + 1, 1).Value = "Plan/Actual = " & planValue & " / " & actualValue & " Ton"
row = row + 2   ' naik 2 baris untuk output berikutnya

End Sub






Function GetAttachment_BIO(targetDate As Date) As String
    Dim olApp As Object, olNs As Object, olFolder As Object
    Dim olItems As Object, olMail As Object, olAtmt As Object
    Dim baseName As String, savePath As String
    Dim i As Long
    
    baseName = "08. Actual vs Planning Agustus Marunda (2025) 1.xlsx"
    savePath = Environ("TEMP") & "\BIO_" & Format(Date, "yyyymmdd") & ".xlsx"
    
    Set olApp = CreateObject("Outlook.Application")
    Set olNs = olApp.GetNamespace("MAPI")
    Set olFolder = olNs.GetDefaultFolder(6)
    Set olItems = olFolder.Items
    olItems.Sort "[ReceivedTime]", True
    
    For i = 1 To olItems.Count
        Set olMail = olItems.Item(i)
        If olMail.Class = 43 Then
            If Int(olMail.ReceivedTime) = Int(Date) Then
                If InStr(1, olMail.senderName, "Jessica Silvera", vbTextCompare) > 0 Or _
                   InStr(1, olMail.senderName, "Wendy Fadillah Nugraha", vbTextCompare) > 0 Then
                    For Each olAtmt In olMail.Attachments
                        If olAtmt.Filename = baseName Then
                            olAtmt.SaveAsFile savePath
                            GetAttachment_BIO = savePath
                            Exit Function
                        End If
                    Next olAtmt
                End If
            End If
        End If
    Next i
    GetAttachment_BIO = ""
End Function


Function GetCellVal(ws As Worksheet, targetRow As Long, _
                    header1 As String, header2 As String, _
                    header3 As String, header4 As String, _
                    Optional headerRow1 As Long = 7, _
                    Optional headerRow2 As Long = 8, _
                    Optional headerRow3 As Long = 9, _
                    Optional headerRow4 As Long = 10) As Double

    Dim col As Long, lastCol As Long
    Dim val1 As String, val2 As String, val3 As String, val4 As String
    Dim h1 As String, h2 As String, h3 As String, h4 As String

    On Error GoTo ErrorHandler

    lastCol = ws.Cells(headerRow1, ws.Columns.Count).End(xlToLeft).Column

    h1 = LCase(Trim(header1))
    h2 = LCase(Trim(header2))
    h3 = LCase(Trim(header3))
    h4 = LCase(Trim(header4))

    For col = 1 To lastCol
        val1 = LCase(Trim(GetMergedHeaderValue(ws, headerRow1, col)))
        val2 = LCase(Trim(GetMergedHeaderValue(ws, headerRow2, col)))
        val3 = LCase(Trim(GetMergedHeaderValue(ws, headerRow3, col)))
        val4 = LCase(Trim(GetMergedHeaderValue(ws, headerRow4, col)))

        ' Khusus Crude Fame: baris 8 & 9 merge (anggap val2 kosong, hanya gunakan val3)
        If InStr(val1, "crude fame") > 0 And _
           InStr(val3, h3) > 0 And InStr(val4, h4) > 0 Then
            GetCellVal = CDbl(ws.Cells(targetRow, col).Value)
            Exit Function
        End If

        ' Umum: semua header cocok secara longgar
        If InStr(val1, h1) > 0 And InStr(val2, h2) > 0 And _
           InStr(val3, h3) > 0 And InStr(val4, h4) > 0 Then
            GetCellVal = CDbl(ws.Cells(targetRow, col).Value)
            Exit Function
        End If
    Next col

    GetCellVal = 0
    Exit Function

ErrorHandler:
    Debug.Print "Hasil GetCellVal: ERROR"
    GetCellVal = 0
End Function




Function GetMergedHeaderValue(ws As Worksheet, headerRow As Long, col As Long) As String
    Dim cell As Range
    Set cell = ws.Cells(headerRow, col)

    ' Jika sel kosong, cek apakah bagian dari merge
    If IsEmpty(cell.Value) And cell.MergeCells Then
        GetMergedHeaderValue = cell.MergeArea.Cells(1, 1).Value
    Else
        GetMergedHeaderValue = cell.Value
    End If
End Function




' Fungsi bantu untuk mengambil nilai header dengan mempertimbangkan merged cells
Function GetMergedHeader(ws As Worksheet, headerRow As Long, col As Long) As String
    Dim val As String
    val = Trim(ws.Cells(headerRow, col).Value)
    
    If val = "" Then
        ' Cari ke atas jika cell kosong karena merged
        Do While val = "" And headerRow > 1
            headerRow = headerRow - 1
            val = Trim(ws.Cells(headerRow, col).Value)
        Loop
    End If
    
    GetMergedHeader = val
End Function





Function GetTextVal(ws As Worksheet, targetRow As Long, ParamArray headers() As Variant) As String
    Dim headerRow1 As Long: headerRow1 = 8
    Dim headerRow2 As Long: headerRow2 = 9
    Dim col As Long
    Dim i As Long, j As Long
    Dim lastCol As Long: lastCol = ws.Cells(headerRow1, ws.Columns.Count).End(xlToLeft).Column

    For col = 1 To lastCol
        j = 0
        For i = LBound(headers) To UBound(headers)
            Dim headerText As String
            If j Mod 2 = 0 Then
                headerText = LCase(Trim(ws.Cells(headerRow1, col).Value))
            Else
                headerText = LCase(Trim(ws.Cells(headerRow2, col).Value))
            End If

            If InStr(1, headerText, LCase(headers(i)), vbTextCompare) > 0 Then
                j = j + 1
            Else
                Exit For
            End If
        Next i

        If j = UBound(headers) + 1 Then
            GetTextVal = Trim(ws.Cells(targetRow, col).Value)
            Exit Function
        End If
    Next col

    GetTextVal = "" ' Default jika tidak ketemu
End Function

' ==== Fungsi Cari Baris Tanggal ====
Function FindMatchingRows(ws As Worksheet, colDate As String, startRow As Long, targetDate As Date) As Collection
    Dim matchingRows As New Collection
    Dim lastRow As Long, i As Long
    Dim cell As Range, mergeVal As Variant
    
    lastRow = ws.Cells(ws.Rows.Count, colDate).End(xlUp).row
    
    For i = startRow To lastRow
        Set cell = ws.Cells(i, colDate)
        If cell.MergeCells Then
            mergeVal = cell.MergeArea.Cells(1, 1).Value
        Else
            mergeVal = cell.Value
        End If
        
        If IsDate(mergeVal) Then
            ' Bandingkan hanya tanggalnya (abaikan jam)
            If Int(CDate(mergeVal)) = targetDate Then
                matchingRows.Add i
            End If
        End If
    Next i
    
    Set FindMatchingRows = matchingRows
End Function

